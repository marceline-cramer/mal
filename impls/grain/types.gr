module Types

include "list"
include "map"
include "option"
include "string"

provide record Env {
  outer: Option<Env>,
  data: Map.Map<String, MalType>,
},

provide enum MalType {
  Nil,
  Bool(Bool),
  String(String),
  Symbol(String),
  Keyword(String),
  Number(Number),
  List(List<MalType>),
  Vector(List<MalType>),
  Atom(Box<MalType>),
  Function((List<MalType>, Env) -> Result<MalType, String>),
}

provide let rec pr_str = (val, print_readably) => {
  match (val) {
    Nil => "nil",
    Bool(true) => "true",
    Bool(false) => "false",
    String(str) when print_readably => {
      let str = String.replaceAll("\"", "\\\"", str)
      let str = String.replaceAll("\n", "\\n", str)
      "\"" ++ str ++ "\""
    },
    String(str) => str,
    Keyword(str) => str,
    Symbol(sym) => sym,
    Number(num) => toString(num),
    Vector(vector) => {
      let strs = List.map((elem) => pr_str(elem, print_readably), vector)
      let joined = List.join(" ", strs)
      "[" ++ joined ++ "]"
    },
    List(list) => {
      let strs = List.map((elem) => pr_str(elem, print_readably), list)
      let joined = List.join(" ", strs)
      "(" ++ joined ++ ")"
    },
    Atom(val) => "(atom " ++ pr_str(unbox(val), print_readably) ++ ")",
    Function(_) => "<function>",
  }
}

provide let set = (key, val, env) => {
  Map.set(key, val, env.data)
  val
}

provide let rec find = (key, env) => {
  match (Map.get(key, env.data)) {
    Some(val) => Ok(val),
    None => match (env.outer) {
      Some(outer) => find(key, outer),
      None => Err("'" ++ key ++ "' not found."),
    }
  }
}

provide let get = (key, env) => {
  Option.toResult("'" ++ key ++ "' not found.", Map.get(key, env.data))
}

provide let make_inner = (env) => {
  { outer: Some(env), data: Map.make() }
}
